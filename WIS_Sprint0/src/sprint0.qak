System wis_system

Dispatch	actor_state					: actor_state(P,V)
Request 	conditions_verified_req		: conditions_verified_req(N)
Reply 		conditions_verified_repl	: conditions_verified_repl(R) for conditions_verified_req

Request rp_req						: rp_req(N)
Reply   rp_repl						: rp_repl(N) for rp_req

Request burn_req					: burn_req(N)
Reply 	burn_repl					: burn_repl(N) for burn_req

Request ash_req						: ash_req(N)
Reply	ash_repl					: ash_repl(N) for ash_req





Context ctx_wis ip [ host="localhost" port=8020]  

QActor incinerator context ctx_wis{     
 	[#
 		var BURNING    = false;
 		var BURNOUT_FREE = true;		
 	 #]
 	 
 	State s0 initial{ 
 		println("$name starts") color red
		delay 500
		updateResource  [# "actor_state(incinerator_burning,$BURNING)" #]
	}Goto idle
	
	State idle{
		println("$name: idle") color red
	}Transition t0
	whenRequest burn_req -> handle_burn_req
	
	State handle_burn_req{
		[#
			var R=false
			if(!BURNING){
				BURNING=true
				R=true
			}
			
		#]
		println("$name: handling burn request, result:$BURNING") color red

		if[# BURNING #]{
			updateResource  [# "actor_state(incinerator_burning,$BURNING)" #]
			delay 5000 // burning simulation
			[# 
				BURNING=false
				BURNOUT_FREE=false
			#]
			updateResource  [# "actor_state(incinerator_burning,$BURNING)" #]
		}
		replyTo burn_req with burn_repl : burn_repl($R)
	}Goto idle
}
 
QActor waste_storage context ctx_wis{
 	[#
 		var ROLL_PACKETS=10
 	 #]
 	 
 	State s0 initial{ 
 		println("$name starts") color blue
		delay 500
		updateResource [# "actor_state(waste_storage_rps,$ROLL_PACKETS)" #]
	}Goto idle
	
	State idle{
		println("$name: idle") color blue
	}Transition t0 
	whenRequest rp_req -> handle_rp_req
	
	State handle_rp_req{
		[#
			var R=false
			if(ROLL_PACKETS>0){
				R=true
				ROLL_PACKETS--
			}
			
		#]
		println("$name: handling rp request, result:$R") color blue
		
		updateResource [# "actor_state(waste_storage_rps,$ROLL_PACKETS)" #]
		replyTo rp_req with rp_repl : rp_repl($R)
	}Goto idle
	
 }
 
QActor ash_storage context ctx_wis{
 	[#
 		var ASH_LEVEL=0
 		val MAX_ASH_LEVEL=3
 	 #]
 	
 	 State s0 initial{ 
 		println("$name starts") color magenta
		delay 500
		updateResource [# "actor_state(ash_storage_level,$ASH_LEVEL)" #]
	} Goto idle
	
		
	State idle{
		println("$name: idle") color magenta
	}Transition t0 
	whenRequest ash_req -> handle_ash_req
	
	State handle_ash_req{
		[#
			var R=false
			if(ASH_LEVEL<MAX_ASH_LEVEL){
				R=true
				ASH_LEVEL++
			}
			
		#]
		println("$name: handling ash request, result:$R") color magenta
		
		updateResource [# "actor_state(ash_storage_level,$ASH_LEVEL)" #]
		replyTo ash_req with ash_repl : ash_repl($R)
	}Goto idle
	
}

QActor op_robot context ctx_wis{
	[#
		var POSITION="home"
		var OK=false;
	#]
	State s0 initial{ 
 		println("$name starts") color green
		delay 500
	}Goto check_start_conditions
	
	State check_start_conditions{
		delay 500
		println("$name: checking conditions") color green
		request waste_incinerator_service -m conditions_verified_req:conditions_verified_req
	}Transition t0
	whenReply conditions_verified_repl->handle_start_conditions_verified_repl
	
	State handle_start_conditions_verified_repl{
		onMsg(conditions_verified_repl:conditions_verified_repl(R)){
			[#
				OK=payloadArg(0).toBoolean()
			#]
		}
		println("$name: conditions verified: $OK") color green
	}Goto move_to_waste_storage if [# OK #] else check_start_conditions
	
	
	// loading an rp
	State move_to_waste_storage{
		println("$name: moving to waste storage...") color green
		delay 2000
	} Goto request_rp_to_waste_storage
	
	State request_rp_to_waste_storage{
		println("$name: requesting an rp") color green
		request waste_storage -m rp_req:rp_req
	}Transition t0
	whenReply rp_repl -> handle_rp_repl
	
	State handle_rp_repl{
		[#
			OK=payloadArg(0).toBoolean()	
		#]
		println("$name: rp request result: $OK")
	}Goto move_to_incinerator
	
	
	
	// burning the rp
	State move_to_incinerator{
		println("$name: moving to incinerator...") color green
		delay 2000
	}Goto request_to_burn 
	
	State request_to_burn{
		println("$name: requesting to burn an rp") color green
		request incinerator -m burn_req : burn_req
	}Transition t0
	whenReply burn_repl -> handle_burn_repl
	
	State handle_burn_repl{
		[#
			OK=payloadArg(0).toBoolean()	
		#]
		println("$name: burn request result: $OK") color green
	}Goto move_to_ash_storage
	
	
	// unload ashes
	State move_to_ash_storage{
		println("$name: moving to ash storage...") color green
		delay 2000
	}Goto request_to_unload_ash
	
	 State request_to_unload_ash{
	 	println("$name: requesting to unload ash...") color green
	 	request ash_storage -m ash_req : ash_req
	 }Transition t0
	 whenReply ash_repl -> handle_ash_repl
	 
	 State handle_ash_repl{
	 	[#
			OK=payloadArg(0).toBoolean()	
		#]
		println("$name: request to unload ash result: $OK") color green
	 }Goto check_continue_conditions
	 
	 State check_continue_conditions{
		println("$name: checking conditions") color green
		request waste_incinerator_service -m conditions_verified_req:conditions_verified_req
	}Transition t0
	whenReply conditions_verified_repl->handle_continue_conditions_verified_repl
	
	State handle_continue_conditions_verified_repl{
		onMsg(conditions_verified_repl:conditions_verified_repl(R)){
			[#
				OK=payloadArg(0).toBoolean()
			#]
		}
		println("$name: conditions verified: $OK") color green
	}Goto move_to_waste_storage if [#OK#] else move_to_home
	
	State move_to_home{
		println("moving to home...") color green
		delay 2000
	}Goto check_start_conditions
}

QActor waste_incinerator_service context ctx_wis{
 	[#
 		var RP = 0
 		var B = false
 		var L = 0
 		val LMAX = 3
 	
 	#]
 	
 	State s0 initial{ 
 		println("$name starts") color yellow
		observeResource waste_storage msgid actor_state
		observeResource incinerator msgid actor_state
		observeResource ash_storage msgid actor_state  
		
	} Goto waiting_for_updates
	
	State waiting_for_updates{
		println("$name: waiting for updates...") color yellow
	}Transition t0 
		whenMsg actor_state -> update_state
		whenRequest conditions_verified_req -> handle_conditions_verified_req
		
	State update_state{
		onMsg(actor_state:actor_state(P,V)){
			[#
				val P=payloadArg(0);
				val V=payloadArg(1);
				when (P){
					"incinerator_burning"-> B=V.toBoolean()
					"waste_storage_rps"  -> RP=V.toInt()
					"ash_storage_level"  -> L=V.toInt()
				}
			#]
			println("$name: $P updated") color yellow
		}
	} Goto waiting_for_updates


	State handle_conditions_verified_req{
		[#
			val RESULT=(RP>0) && (!B) && (L<LMAX)
		#]
		println("$name: current state { RP:$RP, B:$B, L:$L }, conditions verified: $RESULT")
		replyTo conditions_verified_req with conditions_verified_repl : conditions_verified_repl($RESULT)
	}Goto waiting_for_updates
	
 }

